buildscript {
    ext.kotlinVersion = '1.3.10'
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'com.ofg:uptodate-gradle-plugin:1.6.2'

        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.0'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects { project ->
    group = MODULE_GROUP
    version = MODULE_VERSION

    apply plugin: 'checkstyle'

    task checkstyle(type: Checkstyle) {
        configFile rootProject.file('checkstyle.xml')
        source 'src/main/java'
        ignoreFailures false
        showViolations true
        include '**/*.java'

        classpath = files()
    }

    afterEvaluate {
        if (project.tasks.findByName('check')) {
            check.dependsOn('checkstyle')
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

ext {
    minSdkVersion = 15
    targetSdkVersion = 27
    compileSdkVersion = 27
    buildToolsVersion = '27.0.3'
    sourceCompatibilityVersion = JavaVersion.VERSION_1_7
    targetCompatibilityVersion = JavaVersion.VERSION_1_7


}

ext.deps = [
        gson          : 'com.google.code.gson:gson:2.8.2',
        moshi         : "com.squareup.moshi:moshi:1.8.0",
        conceal       : 'com.facebook.conceal:conceal:1.1.3@aar',

        // Test dependencies
        junit         : 'junit:junit:4.12',
        truth         : 'com.google.truth:truth:0.28',
        robolectric   : 'org.robolectric:robolectric:3.3',
        mockito       : 'org.mockito:mockito-core:2.8.9',
        espressoRunner: 'com.android.support.test:runner:0.5',
        espressoRules : 'com.android.support.test:rules:0.5',
        kotlin        : "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"

]


